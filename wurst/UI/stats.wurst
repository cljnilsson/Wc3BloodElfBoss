package stats

import ClosureTimers
import LinkedListModule
import info

framehandle parent
trigger buttonTrigger = CreateTrigger()

class Data
	use LinkedListModule

	string icon
	string desc
	string label

	framehandle fIcon
	framehandle fText
	framehandle fButton
	framehandle fToolTip

	int i

	construct(string label, string icon, string desc)
		this.label = label	
		this.icon = icon
		this.desc = desc

		this.fButton 	= BlzGetFrameByName("CustomUnitInfoButton" + I2S(size), 0)
		this.fToolTip 	= BlzCreateFrameByType("SIMPLEFRAME", "", this.fButton, "", 0)
		this.fIcon   	= BlzGetFrameByName("CustomUnitInfoButtonIcon" + I2S(size), 0)
		this.fText		= BlzGetFrameByName("CustomUnitInfoButtonText" + I2S(size), 0)

		i = size

		BlzTriggerRegisterFrameEvent(buttonTrigger, fButton, FRAMEEVENT_CONTROL_CLICK)
		BlzFrameSetTooltip(fButton, fToolTip)
		fToolTip.hide()
		fIcon.setTexture(icon, 0, false)

		unitInfoPanelAddTooltipListener(fToolTip, function tooltipAction)

function tooltipAction()
	for d from Data.iterator()
		if unitInfoTooltipFrame == d.fToolTip
			unitInfoTooltipText = d.label + BlzFrameGetText(d.fText) + "\n" + d.desc
			break

function condition() returns boolean
	return IsUnitType(unitInfoUnit, UNIT_TYPE_HERO)

 function buttonAction()
	GetTriggerPlayer().unitInfoGetUnit()

function update()
	if BlzFrameIsVisible(parent)
		for d from Data.iterator()
			string s = ""

			switch(d.i)
				case 1
					s = BlzFrameGetText(BlzGetFrameByName("InfoPanelIconValue", 0))
				case 2
					s = "0"
				case 3
					s = GetUnitMoveSpeed(unitInfoUnit).toString()
				case 4
					s = BlzFrameGetText(BlzGetFrameByName("InfoPanelIconHeroStrengthValue", 6))
				case 5
					s = BlzFrameGetText(BlzGetFrameByName("InfoPanelIconHeroAgilityValue", 6))
				case 6
					s = BlzFrameGetText(BlzGetFrameByName("InfoPanelIconHeroIntellectValue", 6))
				case 7
					s = R2SW(BlzGetUnitRealField(unitInfoUnit, UNIT_RF_HIT_POINTS_REGENERATION_RATE), 1, 1)
				case 8
					s = R2SW(BlzGetUnitRealField(unitInfoUnit, UNIT_RF_MANA_REGENERATION), 1, 1)

			BlzFrameSetText(d.fText, s)

public function onStatsInit()
	BlzGetFrameByName("InfoPanelIconValue"              , 0)
	BlzGetFrameByName("InfoPanelIconValue"              , 2)
	BlzGetFrameByName("InfoPanelIconHeroStrengthValue"  , 6)
	BlzGetFrameByName("InfoPanelIconHeroAgilityValue"   , 6)
	BlzGetFrameByName("InfoPanelIconHeroIntellectValue" , 6)

	parent = BlzCreateSimpleFrame("CustomUnitInfoPanel2x4", BlzGetFrameByName("SimpleInfoPanelUnitDetail", 0), 0)
	addUnitInfoPanel(parent, function update, function condition)

	new Data("Damage: " , "ReplaceableTextures\\CommandButtons\\BTNSteelMelee"			, "The amount of damage your basic Attack deals")
	new Data("Armor: "  , "ReplaceableTextures\\CommandButtons\\BTNHumanArmorUpOne"		, "Reduces Taken non magical damage")
	new Data("Speed: "  , "ReplaceableTextures\\CommandButtons\\BTNBootsOfSpeed"		, "The unit's current movespeed")
	new Data("Str: "    , "ReplaceableTextures\\CommandButtons\\BTNGauntletsOfOgrePower", "Increases Life and Liferegeneration")
	new Data("Agi: "    , "ReplaceableTextures\\CommandButtons\\BTNSlippersOfAgility"	, "Improves Armor and Attackspeed")
	new Data("Int: "    , "ReplaceableTextures\\CommandButtons\\BTNMantleOfIntelligence", "Improves Mana and Manaregeneration")
	new Data("Hp/s: "   , "ReplaceableTextures\\CommandButtons\\BTNRegenerate"			, "")
	new Data("Mp/s: "   , "ReplaceableTextures\\CommandButtons\\BTNMagicalSentry"		, "")

init
	TriggerAddAction(buttonTrigger, function buttonAction)